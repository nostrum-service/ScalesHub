; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Nostrum Scales Hub"
#define NostrumAppName "Nostrum Scales Hub"
#define NostrumGroupName "Nostrum"
#define MyAppVersion "1.0"
#define MyAppPublisher "Nostrum"
#define MyAppURL "http://www.nostrum.ru/"
#define MyAppExeName "ScalesHubConsole.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppID={{9E255519-FFC6-4F64-B919-23382A9CDF0D}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=true
OutputDir=E:\work\ScalesHub\InnoSetup
OutputBaseFilename=setup
SetupIconFile=E:\work\ScalesHub\nostrum.ico
Compression=lzma/Max
SolidCompression=true
VersionInfoCompany=Nostrum

[Languages]
Name: english; MessagesFile: compiler:Default.isl
Name: russian; MessagesFile: compiler:Languages\Russian.isl

[Tasks]
Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked; Components: 
Name: quicklaunchicon; Description: {cm:CreateQuickLaunchIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked; OnlyBelowVersion: 0,6.1; Components: 

[Files]

Source: E:\work\ScalesHub\nostrum.ico; DestDir: {app}; Flags: replacesameversion; 

Source: C:\Program Files (x86)\DevExpress 14.2\Components\Bin\Framework\DevExpress.Data.v14.2.dll; DestDir: {app}; Flags: replacesameversion; 

Source: C:\Program Files (x86)\DevExpress 14.2\Components\Bin\Framework\DevExpress.BonusSkins.v14.2.dll; DestDir: {app}; Flags: replacesameversion; Components: 

Source: C:\Program Files (x86)\DevExpress 14.2\Components\Bin\Framework\DevExpress.Utils.v14.2.dll; DestDir: {app}; Flags: replacesameversion; Components: 

Source: C:\Program Files (x86)\DevExpress 14.2\Components\Bin\Framework\DevExpress.Images.v14.2.dll; DestDir: {app}; Flags: replacesameversion; Components: 

Source: C:\Program Files (x86)\DevExpress 14.2\Components\Bin\Framework\DevExpress.XtraBars.v14.2.dll; DestDir: {app}; Flags: replacesameversion; Components: 
Source: C:\Program Files (x86)\DevExpress 14.2\Components\Bin\Framework\DevExpress.XtraEditors.v14.2.dll; DestDir: {app}; Flags: replacesameversion; Components: 

Source: C:\Program Files (x86)\DevExpress 14.2\Components\Bin\Framework\DevExpress.Sparkline.v14.2.Core.dll; DestDir: {app}; Flags: replacesameversion; Components: 

Source: C:\Program Files (x86)\DevExpress 14.2\Components\Bin\Framework\DevExpress.XtraGrid.v14.2.dll; DestDir: {app}; Flags: replacesameversion; Components: 

Source: C:\Program Files (x86)\DevExpress 14.2\Components\Bin\Framework\DevExpress.XtraLayout.v14.2.dll; DestDir: {app}; Flags: replacesameversion; Components: 

Source: C:\Program Files (x86)\DevExpress 14.2\Components\Bin\Framework\DevExpress.XtraNavBar.v14.2.dll; DestDir: {app}; Flags: replacesameversion; Components: 

Source: C:\Program Files (x86)\DevExpress 14.2\Components\Bin\Framework\DevExpress.XtraTreeList.v14.2.dll; DestDir: {app}; Flags: replacesameversion; Components: 

Source: C:\Program Files (x86)\DevExpress 14.2\Components\Bin\Framework\DevExpress.XtraPrinting.v14.2.dll; DestDir: {app}; Flags: replacesameversion; Components: 
Source: C:\Program Files (x86)\DevExpress 14.2\Components\Bin\Framework\DevExpress.Printing.v14.2.Core.dll; DestDir: {app}; Flags: replacesameversion; Components: 


Source: C:\Program Files (x86)\DevExpress 14.2\Components\Bin\Framework\ru\DevExpress.Data.v14.2.resources.dll; DestDir: {app}\ru
Source: C:\Program Files (x86)\DevExpress 14.2\Components\Bin\Framework\ru\DevExpress.Utils.v14.2.resources.dll; DestDir: {app}\ru

Source: C:\Program Files (x86)\DevExpress 14.2\Components\Bin\Framework\ru\DevExpress.XtraBars.v14.2.resources.dll; DestDir: {app}\ru
Source: C:\Program Files (x86)\DevExpress 14.2\Components\Bin\Framework\ru\DevExpress.XtraEditors.v14.2.resources.dll; DestDir: {app}\ru
Source: C:\Program Files (x86)\DevExpress 14.2\Components\Bin\Framework\ru\DevExpress.XtraGrid.v14.2.resources.dll; DestDir: {app}\ru
Source: C:\Program Files (x86)\DevExpress 14.2\Components\Bin\Framework\ru\DevExpress.XtraLayout.v14.2.resources.dll; DestDir: {app}\ru
Source: C:\Program Files (x86)\DevExpress 14.2\Components\Bin\Framework\ru\DevExpress.XtraNavBar.v14.2.resources.dll; DestDir: {app}\ru

Source: C:\Program Files (x86)\DevExpress 14.2\Components\Bin\Framework\ru\DevExpress.Printing.v14.2.Core.resources.dll; DestDir: {app}\ru 
Source: C:\Program Files (x86)\DevExpress 14.2\Components\Bin\Framework\ru\DevExpress.XtraPrinting.v14.2.resources.dll; DestDir: {app}\ru

Source: C:\Program Files (x86)\DevExpress 14.2\Components\Bin\Framework\ru\DevExpress.XtraTreeList.v14.2.resources.dll; DestDir: {app}\ru

Source: E:\work\ScalesHub\ScalesHubConsole\bin\Release\LiteDB.dll; DestDir: {app}; Components: ; Flags: replacesameversion;
Source: E:\work\ScalesHub\ScalesHubConsole\bin\Release\NLog.dll; DestDir: {app}; Components: ; Flags: replacesameversion;
Source: E:\work\ScalesHub\ScalesHubConsole\bin\Release\Newtonsoft.Json.dll; DestDir: {app}; Components: ; Flags: replacesameversion;
Source: E:\work\ScalesHub\ScalesHubConsole\bin\Release\ScalesHubConsole.exe; DestDir: {app}; Components: ; Flags: replacesameversion;
Source: E:\work\ScalesHub\ScalesHubConsole\bin\Release\ScalesHubConsole.exe.config; DestDir: {app}; Components: ; Flags: onlyifdoesntexist; 

[Icons]
Name: {group}\{#MyAppName}; Filename: {app}\{#MyAppExeName}; WorkingDir: {app}; IconFilename: {app}\nostrum.ico; 
Name: {commondesktop}\{#MyAppName}; Filename: {app}\{#MyAppExeName}; WorkingDir: {app}; IconFilename: {app}\nostrum.ico; Tasks: desktopicon
Name: {userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}; Filename: {app}\{#MyAppExeName}; IconFilename: {app}\nostrum.ico; Tasks: quicklaunchicon
Name: {group}\{cm:UninstallProgram, {#NostrumAppName}}; Filename: {uninstallexe}; Languages: english russian; 

[Run]
Filename: {app}\{#MyAppExeName}; WorkingDir: {app}; Description: {cm:LaunchApplication}; Flags: postinstall Unchecked; 

[CustomMessages]
english.LaunchApplication=Launch application
russian.LaunchApplication=Запустить приложение

[Code]
function IsDotNetDetected(version: string; service: cardinal): boolean;
// Indicates whether the specified version and service pack of the .NET Framework is installed.
//
// version -- Specify one of these strings for the required .NET Framework version:
//    'v1.1.4322'     .NET Framework 1.1
//    'v2.0.50727'    .NET Framework 2.0
//    'v3.0'          .NET Framework 3.0
//    'v3.5'          .NET Framework 3.5
//    'v4\Client'     .NET Framework 4.0 Client Profile
//    'v4\Full'       .NET Framework 4.0 Full Installation
//    'v4.5'          .NET Framework 4.5
//
// service -- Specify any non-negative integer for the required service pack level:
//    0               No service packs required
//    1, 2, etc.      Service pack 1, 2, etc. required
var
    key: string;
    install, release, serviceCount: cardinal;
    check45, success: boolean;
begin
    // .NET 4.5 installs as update to .NET 4.0 Full
    if version = 'v4.5' then begin
        version := 'v4\Full';
        check45 := true;
    end else
        check45 := false;

    // installation key group for all .NET versions
    key := 'SOFTWARE\Microsoft\NET Framework Setup\NDP\' + version;

    // .NET 3.0 uses value InstallSuccess in subkey Setup
    if Pos('v3.0', version) = 1 then begin
        success := RegQueryDWordValue(HKLM, key + '\Setup', 'InstallSuccess', install);
    end else begin
        success := RegQueryDWordValue(HKLM, key, 'Install', install);
    end;

    // .NET 4.0/4.5 uses value Servicing instead of SP
    if Pos('v4', version) = 1 then begin
        success := success and RegQueryDWordValue(HKLM, key, 'Servicing', serviceCount);
    end else begin
        success := success and RegQueryDWordValue(HKLM, key, 'SP', serviceCount);
    end;

    // .NET 4.5 uses additional value Release
    if check45 then begin
        success := success and RegQueryDWordValue(HKLM, key, 'Release', release);
        success := success and (release >= 378389);
    end;

    result := success and (install = 1) and (serviceCount >= service);
end;


function InitializeSetup(): Boolean;
begin
    if not IsDotNetDetected('v4.5', 0) then begin
        MsgBox('Для работы приложения требуется Microsoft .NET Framework 4.5'#13#13
            'Пожалуйста установите Microsoft .NET Framework 4.5'#13
            'и перезапустите программу установки!', mbInformation, MB_OK);
        result := false;
    end else
        result := true;
end;